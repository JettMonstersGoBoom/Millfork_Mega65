
import stdio
import mega.dma 

const byte SCREEN_WIDTH=80
const word SCREEN_SIZE=(SCREEN_WIDTH*25)
const int24 SCREEN_BASE = $0800

void main() {
	byte ticker
	asm {
		sei
	}
	M65_40hz()
	M65_defaultmem()
	M65_VIC4Enable()
	M65_disable_rom()
	M65_dma_prepare()

	//	for 80 col mode
	vic4_charstep = SCREEN_WIDTH 	
	vic4_charcount = SCREEN_WIDTH
	vic4_screen_ptr = SCREEN_BASE

	//	set dma skip to 1 byte 
	//	for a fill 
	DMAJob.scr_skip_lsb = 1	//	make no difference for FILL
	DMAJob.dst_bank = $ff	//	for color ram
	DMAJob.source.lo=$1
	DMAJob.destination = $80000
	DMAJob.size = SCREEN_SIZE
	DMAJob.type = DMA_FILL
	M65_dma_kick()

	//	clear screen 
	DMAJob.dst_bank = $0	
	DMAJob.source.lo=$20
	DMAJob.destination = SCREEN_BASE
	DMAJob.size = SCREEN_SIZE
	DMAJob.type = DMA_FILL
	M65_dma_kick()

	DMAJob.scr_skip_lsb = 1
	DMAJob.dst_skip_lsb = 1
	DMAJob.dst_bank = $00	
	DMAJob.source=hello_world.addr
	DMAJob.destination = SCREEN_BASE
	DMAJob.size = hello_world.length
	DMAJob.type = DMA_COPY

	M65_dma_kick()

	//	skip of 2 on destination 
	DMAJob.dst_skip_lsb = 2
	DMAJob.source=hello_world.addr
	DMAJob.destination = SCREEN_BASE + 80 
	M65_dma_kick()
	//	draw a strip down 
	DMAJob.dst_skip_lsb = vic4_charstep.lo
	DMAJob.source=hello_world.addr
	DMAJob.destination = SCREEN_BASE + 78 
	M65_dma_kick()

	vic_border = 0

	while true {
		//	mix asm wherever you like
		asm 
		{
vbl:
			//	show raster pos as colors
			ldx $d012
			clc 
			txa
			adc ticker
			sta $d100 
			sta $d200 
			sta $d300
			cpx #$f0
			bne vbl
		}
		ticker+=1
	}
}

const array hello_world=["hello mega65, hope you have some fun !  " scr]
